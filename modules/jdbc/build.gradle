/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements. See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License. You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    alias(libs.plugins.shadow)
}

apply from: "$rootDir/buildscripts/java-core.gradle"
apply from: "$rootDir/buildscripts/publishing.gradle"
apply from: "$rootDir/buildscripts/java-junit5.gradle"
apply from: "$rootDir/buildscripts/java-integration-test.gradle"

configurations {
    shaded
    compileOnly {
        extendsFrom(shaded)
    }
    // Add shaded dependencies to test as well
    // This enables to execute unit tests with original (non-shaded dependencies)
    testImplementation {
        extendsFrom(shaded)
    }
    integrationTestImplementation {
        extendsFrom(shaded)
    }
}

dependencies {
    shaded project(':ignite-client')
    shaded project(':ignite-client-common')
    shaded project(':ignite-api')
    shaded project(':ignite-core')

    implementation libs.jetbrains.annotations
    implementation libs.auto.service.annotations

    annotationProcessor libs.auto.service

    integrationTestImplementation(testFixtures(project(":ignite-core")))
    integrationTestImplementation(project(":ignite-runner"))
}

description = 'ignite-jdbc'

shadowJar {
    configurations = [project.configurations.shaded]

    minimize()

    [
            'io.netty',
            'it.unimi',
            'org.jetbrains',
            'org.msgpack'
    ].forEach {
        relocate(it, "${project.group}.shaded.$it")
    }
}
