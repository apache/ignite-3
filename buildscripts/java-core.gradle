/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'checkstyle'
apply plugin: 'pmd'

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    withSourcesJar()
}

test {
    finalizedBy(jacocoTestReport)
}

pmd {
    ignoreFailures = false
    consoleOutput = true

    toolVersion = libs.versions.pmdTool.get()
    ruleSets = []//switch off old rulesets
}

processResources {
    filesMatching('**/*.properties') {
        filter { String line ->
            line.replace("\${project.version}", project.version)
        }
    }
}

tasks.withType(Pmd) {
    reports {
        html {
            required = true
        }
        xml {
            required = true
        }
    }
}

pmdMain {
    enabled = false
    ruleSetFiles = files("$rootDir/check-rules/pmd-rules.xml")
}

pmdTest {
    enabled = false
    ruleSetFiles = files("$rootDir/check-rules/pmd-rules.xml")
}


checkstyle {
    toolVersion = libs.versions.checkstyleTool.get()
    ignoreFailures = false
    showViolations = true
    maxWarnings = 0
    configFile = file("$rootDir/check-rules/checkstyle-rules.xml")
    configProperties.
    configProperties = [
            "checkstyle.header.file"      : file("$rootDir/check-rules/LICENSE.txt"),
            "checkstyle.suppressions.file": file("$rootDir/check-rules/checkstyle-suppressions.xml")
    ]
}

tasks.withType(Checkstyle) {
    enabled false
    reports {
        html {
            required = true
        }
        xml {
            required = true
        }
    }
}

jacoco {
    toolVersion = libs.versions.jacocoTool.get()
}

jacocoTestReport {
    reports {
        html {
            required = true
        }
        xml {
            required = true
        }
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ['org/apache/calcite/**/*'])
        }))
    }
}