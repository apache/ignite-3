/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements. See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License. You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java'
    id 'application'
    alias(libs.plugins.nebula)
}

configurations {
    dbArtifacts
}

import org.apache.tools.ant.filters.ReplaceTokens

dependencies {
    dbArtifacts(project(':ignite-runner'))
}

def javaArgs = '-Dio.netty.tryReflectionSetAccessible=true ' +
        '-Djava.util.logging.config.file=$CONFIG_PATH/ignite.java.util.logging.properties'

def args = '--config-path $IGNITE_CONFIG_FILE --work-dir $WORK_PATH $NODE_NAME'

def tokens = [
        PRODUCT_NAME        : 'ignite3db',
        PRODUCT_DISPLAY_NAME: 'Apache Ignite',
        APP_JAR             : 'ignite-runner-3.0.0-SNAPSHOT.jar', //FIXME
        MAIN_CLASS          : 'org.apache.ignite.app.IgniteCliRunner', //FIXME
        ADDITIONAL_OPTS     : '',
        JAVA_OPTS           : javaArgs,
        ARGS                : args
]

def zipTokens = tokens + [
        CONF_DIR            : '${CONFIG_PATH}',
        LOG_DIR             : '${LOGS_PATH}',
        IGNITE_WORK_DIR     : '${WORK_PATH}',
        LIB_DIR             : '${LIBS_PATH}',
        IGNITE_CONF_FILE    : 'ignite-config.conf',
        ENV_FILE            : '${IGNITE_HOME}/etc/bootstrap-config.env',
]

def packageTokens = tokens + [
        USERNAME            : 'ignite3',
        GROUPNAME           : 'ignite3',
        INSTALL_DIR         : '/usr/share/ignite3db',
        CONF_DIR            : '/usr/share/ignite3db/etc',
        LOG_DIR             : '/var/log/ignite3db',
        PID_DIR             : '/var/run/ignite3db',
        LIB_DIR             : '/usr/share/ignite3db/lib',
        IGNITE_WORK_DIR     : '/usr/share/ignite3db/work',
        VARS_FILE_NAME      : 'vars.env',
        IGNITE_CONF_FILE    : 'ignite-config.conf',
        ENV_FILE            : '/usr/share/ignite3db/etc/vars.env'
]

task replaceZipScriptVars(type: Copy) {
    from("${rootDir}/packaging/start.sh")
    filter(ReplaceTokens, tokens: zipTokens)
    into("${buildDir}/packaging/zip")
    fileMode 0755
}

task generateZipControlScript(type: Copy) {

    from("${buildDir}/packaging/zip/start.sh")
}

generateZipControlScript.dependsOn replaceZipScriptVars

distributions {
    main {
        distributionBaseName = 'ignite3-db'
        contents {
            // create empty dirs that are required to start Ignite
            into('') {
                File.createTempDir().with {
                    ["log", "etc", "work"].each { new File(absolutePath, it).mkdirs() }
                    from(absolutePath) {
                        includeEmptyDirs = true
                    }
                }
            }
            into('') {
                from("$rootDir/LICENSE")
                from("$rootDir/NOTICE")
                from("$rootDir/assembly/README.md")
            }
            into('etc') {
                from("$projectDir/config/bootstrap-config.env")
                from("$projectDir/config/ignite-config.conf")
                from("$projectDir/config/ignite.java.util.logging.properties")
            }
            into('bin') {
                fileMode 0755
                from("$buildDir/packaging/zip/ignite3db.sh")
            }
            into('lib') {
                from configurations.dbArtifacts
            }
        }
    }
}

distTar.dependsOn replaceZipScriptVars
distZip.dependsOn replaceZipScriptVars

task replacePackageScriptVars(type: Copy) {
    from("${rootDir}/packaging/linux")
    from("${rootDir}/packaging/db/ignite.java.util.logging.properties")
    filter(ReplaceTokens, tokens: packageTokens)
    into("${buildDir}/linux")
}

buildRpm {
    dependsOn replacePackageScriptVars
    configurationFile = "/etc/ignite3/vars.env"

    installUtils file("${buildDir}/linux/service/vars.env")
    installUtils file("${buildDir}/linux/common.sh")
    preInstall file("${buildDir}/linux/preInstall.sh")
    postInstall file("${buildDir}/linux/postInstall.sh")
    preUninstall file("${buildDir}/linux/rpm/preUninstall.sh")
    postUninstall file("${buildDir}/linux/rpm/postUninstall.sh")
}

buildDeb {
    dependsOn replacePackageScriptVars
    configurationFile = "/etc/ignite3/vars.env"

    installUtils file("${buildDir}/linux/service/vars.env")
    installUtils file("${buildDir}/linux/common.sh")
    preInstall file("${buildDir}/linux/preInstall.sh")
    postInstall file("${buildDir}/linux/postInstall.sh")
    preUninstall file("${buildDir}/linux/deb/preUninstall.sh")
    postUninstall file("${buildDir}/linux/deb/postUninstall.sh")
}

ospackage {
    license "ASL 2.0"
    packageName packageTokens.PRODUCT_NAME
    packageGroup "System Environment/Daemons"
    url "https://ignite.apache.org"
    user packageTokens.USERNAME
    packageDescription "This package will install Apache Ignite"
    os LINUX

    into(packageTokens.INSTALL_DIR) {
        into("") {
            from "${buildDir}/linux/service/ignite3db.service"
            from "${buildDir}/linux/service/ignite3db.conf"
            from("${buildDir}/linux/start.sh") {
                fileMode 0755
            }
        }

        into("lib") {
            from configurations.dbArtifacts
        }

        into("etc") {
            fileType CONFIG
            from "${buildDir}/linux/service/vars.env"
            from "${buildDir}/linux/ignite.java.util.logging.properties"
            from "${rootDir}/packaging/config/ignite-config.conf"
        }

        into("etc") {
            from sourceSets.main.resources
        }
    }
    link "/etc/ignite3db", "${packageTokens.INSTALL_DIR}/etc/"
    link "/opt/ignite3db", "${packageTokens.INSTALL_DIR}"
}
